//
// Longest common subsequence problem.
//

int m;
int n;

int ROWS = 20;
int COLS = 20;

int c[400]; //2d array
char b[400]; //2d array

char get_2d_char_idx(char[] arr, int rows, int cols, int i, int j) {
	return arr[i * cols + j];
}

void set_2d_char_idx(char[] arr, int rows, int cols, int i, int j, char val) {
	arr[i * cols + j] = val;
}

int get_2d_int_idx(int[] arr, int rows, int cols, int i, int j) {
	return arr[i * cols + j];
}

void set_2d_int_idx(int[] arr, int rows, int cols, int i, int j, int val) {
	arr[i * cols + j] = val;
}

int strlen(char[] str) {
    int i;
    for (i=0; i<#str; i++) {
        if (str[i] == '\0')
            break;
    }
    return i;
}

void print(char[] x, int i, int j) {
	if (i == 0 || j == 0)
		return;
	if (get_2d_char_idx(b, ROWS, COLS, i, j) == 'c') {
		print(x, i-1,j-1);
		printc(x[i-1]);
	} else if (get_2d_char_idx(b, ROWS, COLS, i, j) == 'u') {
		print(x, i - 1, j);
	} else {
		print(x, i, j - 1);
	}
}


//
// Outputs a character to standard output with a new line appended.
//
void printlnc(char c) {
	printc(c);
	prints("\n");
}


void longest_common_subsequence(char[] x, char[] y) {
	int i;
	int j;
	m = strlen(x);
	n = strlen(y);
	
	for (i = 0; i <= m; i++)
		set_2d_int_idx(c, ROWS, COLS, i, 0, 0);
	for (i = 0; i <= n; i++)
		set_2d_int_idx(c, ROWS, COLS, 0, i, 0);
	
	for (i = 1; i <= m; i++) {
		for (j = 1; j <= n; j++) {
			if (x[i - 1] == y[j - 1]) {
				int prev;
				prev = get_2d_int_idx(c, ROWS, COLS, i - 1, j - 1) + 1;
				set_2d_int_idx(c, ROWS, COLS, i, j, prev);
				set_2d_char_idx(b, ROWS, COLS, i, j, 'c');
			} else if (get_2d_int_idx(c, ROWS, COLS, i - 1, j) >= get_2d_int_idx(c, ROWS, COLS, i, j - 1)) {
				int prev;
				prev = get_2d_int_idx(c, ROWS, COLS, i - 1, j);
				set_2d_int_idx(c, ROWS, COLS, i, j, prev);
				set_2d_char_idx(b, ROWS, COLS, i, j, 'u');
			} else {
				int prev;
				prev = get_2d_int_idx(c, ROWS, COLS, i, j - 1);
				set_2d_int_idx(c, ROWS, COLS, i, j, prev);
				set_2d_char_idx(b, ROWS, COLS, i, j, 'l');
			}
		}
	}
}

char x[25] = "AEDFHR";
char y[25] = "ABCDGH";
void main() {
	prints("The Longest Common Subsequence is ");
	longest_common_subsequence(x, y);
	print(x, m, n);
}

